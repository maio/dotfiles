;; copy of starter-kit-eshell.el --- Saner defaults and goodies: eshell tweaks

(setq eshell-cmpl-cycle-completions nil
      eshell-save-history-on-exit t
      eshell-buffer-shorthand t
      eshell-cmpl-dir-ignore "\\`\\(\\.\\.?\\|CVS\\|\\.svn\\|\\.git\\)/\\'")

;;;###autoload
(with-eval-after-load 'esh-opt
  (require 'em-prompt)
  (require 'em-term)
  (require 'em-cmpl)
  (add-hook 'eshell-mode-hook ;; for some reason this needs to be a hook
            '(lambda ()
               (setq eshell-path-env (getenv "PATH"))
               (define-key eshell-mode-map [tab] 'company-manual-begin)
               (define-key eshell-mode-map "\C-a" 'eshell-bol)))
  (setq eshell-cmpl-cycle-completions nil)

  ;; TODO: submit these via M-x report-emacs-bug
  (add-to-list 'eshell-visual-commands "ssh")
  (add-to-list 'eshell-visual-commands "tail")
  (add-to-list 'eshell-visual-commands "htop")
  (add-to-list 'eshell-command-completions-alist
               '("gunzip" "gz\\'"))
  (add-to-list 'eshell-command-completions-alist
               '("tar" "\\(\\.tar|\\.tgz\\|\\.tar\\.gz\\)\\'")))

;;;###autoload
(defun eshell/cdg ()
  "Change directory to the project's root."
  (eshell/cd (locate-dominating-file default-directory ".git")))

(defface esk-eshell-error-prompt-face
  '((((class color) (background dark)) (:foreground "red" :bold t))
    (((class color) (background light)) (:foreground "red" :bold t)))
  "Face for nonzero prompt results"
  :group 'eshell-prompt)

(add-hook 'eshell-after-prompt-hook
          (defun esk-eshell-exit-code-prompt-face ()
            (when (and eshell-last-command-status
                       (not (zerop eshell-last-command-status)))
              (let ((inhibit-read-only t))
                (add-text-properties
                 (save-excursion (beginning-of-line) (point)) (point-max)
                 '(face esk-eshell-error-prompt-face))))))

(provide 'maio-eshell)
